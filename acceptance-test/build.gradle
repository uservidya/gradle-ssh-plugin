description 'Automated acceptance test with SSH server'

buildscript {
    def productProperties = new Properties()
    file('../gradle.properties').withReader { productProperties.load it }

    repositories {
        ivy { url = '../build/ivy' }
        mavenCentral()
    }

    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:${productProperties.version}"
    }
}

apply from: 'fixture.gradle'

tasks.collectEntries {
    switch (it.name) {
        case ~/^then:/: return [it, tasks.findByName(it.name.replaceFirst(~/^then:/, 'when:'))]
        case ~/^when:/: return [it, tasks.findByName(it.name.replaceFirst(~/^when:/, 'given:'))]
        default:        return [it, null]
    }
}.each { Task self, Task target ->
    if (target) {
        self.dependsOn(target)
    }
}

task test(dependsOn: tasks.findAll { it.name =~ /^then:/ }) {
}
