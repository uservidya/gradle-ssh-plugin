apply plugin: 'ssh'

ssh {
    config(StrictHostKeyChecking: 'no')
}

remotes {
    localhost {
        role 'testServers'
        host = 'localhost'
        user = System.properties['user.name']
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}


task executeCommand(type: SshTask) {
    session(remotes.localhost) {
        execute "touch '${ext.tempFile.path}'"
    }
}

feature('executing a command') {
    given {
        tasks.executeCommand.ext.tempFile = file("${buildDir}/${UUID.randomUUID()}")
        assert !tasks.executeCommand.ext.tempFile.exists()
    }
    when(tasks.executeCommand)
    then {
        assert tasks.executeCommand.ext.tempFile.exists()
        assert tasks.executeCommand.ext.tempFile.delete()
    }
}


task executeCommandByRole(type: SshTask) {
    session(remotes.role('testServers')) {
        execute "touch '${ext.tempFile.path}'"
    }
}

feature('filter hosts by role') {
    given {
        tasks.executeCommandByRole.ext.tempFile = file("${buildDir}/${UUID.randomUUID()}")
        assert !tasks.executeCommandByRole.ext.tempFile.exists()
    }
    when(tasks.executeCommandByRole)
    then {
        assert tasks.executeCommandByRole.ext.tempFile.exists()
        assert tasks.executeCommandByRole.ext.tempFile.delete()
    }
}


task obtainValue(type: SshTask) {
    session(remotes.localhost) {
        ext.z = execute("expr $x + $y")
    }
}

feature('obtaining a value from the remote command') {
    given {
        tasks.obtainValue.ext.x = (Math.random() * 100) as int
        tasks.obtainValue.ext.y = (Math.random() * 100) as int
    }
    when(tasks.obtainValue)
    then {
        assert (tasks.obtainValue.ext.z as int) == (tasks.obtainValue.ext.x + tasks.obtainValue.ext.y)
    }
}
