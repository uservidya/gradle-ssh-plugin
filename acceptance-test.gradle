description 'Automated acceptance test with SSH server'

buildscript {
    repositories {
        ivy { url = "$buildDir/ivy" }
        mavenCentral()
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:$version"
    }
    // avoid dependency resolution problem
    version = null
}

interface FeatureDelegate {
    void given(Closure givenClosure)
    void when(Map map, Closure whenClosure)
    void then(Closure thenClosure)
}

def feature(String name, Closure featureClosure) {
    featureClosure.delegate = [
        given: { Map map = [:], Closure givenClosure -> task(map, "$name@given") << givenClosure },
        when:  { Map map = [:], Closure whenClosure  -> task(map, "$name@when", whenClosure) },
        then:  { Map map = [:], Closure thenClosure  -> task(map, "$name@then") << thenClosure },
    ] as FeatureDelegate
    featureClosure.resolveStrategy = Closure.DELEGATE_FIRST
    featureClosure.call()

    tasks.test.dependsOn(task(name))
    tasks.getByName("$name").dependsOn(tasks.getByName("$name@then"))
    tasks.getByName("$name@then").dependsOn(tasks.getByName("$name@when"))
    tasks.getByName("$name@when").dependsOn(tasks.getByName("$name@given"))
}

task test {
}

apply from: 'acceptance-test.feature.gradle'
