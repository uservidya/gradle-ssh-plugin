description 'Automated acceptance test with SSH server'

buildscript {
    repositories {
        ivy { url = "$buildDir/ivy" }
        mavenCentral()
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:$version"
    }
    // avoid dependency resolution problem
    version = null
}

interface FeatureDelegate {
    void given(Closure givenClosure)
    void when(Task task)
    void then(Closure thenClosure)
    Task getWhenTask()
}

def feature(String name, Closure featureClosure) {
    final given = task("$name@given")
    final when = task("$name@when", dependsOn: given)
    final then = task("$name@then", dependsOn: when)
    tasks.test.dependsOn(then)

    Task whenTask = null
    featureClosure.delegate = [
        given: { Closure c -> given << c },
        then:  { Closure c -> then << c },
        when:  { Task t -> whenTask = t },
        getWhenTask: { -> whenTask }
    ] as FeatureDelegate
    featureClosure.resolveStrategy = Closure.DELEGATE_FIRST
    featureClosure.call()

    when.dependsOn(whenTask)
    whenTask.dependsOn(given)
}

task test {
}

apply from: 'acceptance-test.feature.gradle'
