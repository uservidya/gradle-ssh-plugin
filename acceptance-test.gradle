description 'Automated acceptance test with SSH server'

buildscript {
    repositories {
        ivy { url = "$buildDir/ivy" }
        mavenCentral()
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:$version"
    }
    // avoid dependency resolution problem
    version = null
}

interface FeatureDelegate {
    void given(Closure givenClosure)
    void when(Task whenTask)
    void then(Closure thenClosure)
}

def feature(String name, Closure featureClosure) {
    Closure givenClosure = null
    Closure thenClosure = null
    Task whenTask = null

    featureClosure.delegate = [
        given: { Closure c -> givenClosure = c },
        then:  { Closure c -> thenClosure = c },
        when:  { Task t -> whenTask = t },
    ] as FeatureDelegate
    featureClosure.resolveStrategy = Closure.DELEGATE_FIRST
    featureClosure.call()

    if (whenTask) {
        if (givenClosure) { whenTask.doFirst(givenClosure) }
        if (thenClosure)  { whenTask.doLast(thenClosure) }
        tasks.test.dependsOn(whenTask)
    } else {
        logger.warn "Feature ignored: $name"
    }
}

task test {
}

apply from: 'acceptance-test.feature.gradle'
