description 'Automated acceptance test with SSH server'

buildscript {
    repositories {
        ivy { url = "$buildDir/ivy" }
        mavenCentral()
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:$version"
    }
    // avoid dependency resolution problem
    version = null
}

interface FeatureDelegate {
    void given(Closure givenClosure)
    void when(Task task)
    void then(Closure thenClosure)
}

def feature(String name, Closure featureClosure) {
    def given = task("$name@given")
    def when = task("$name@when", dependsOn: given)
    def then = task("$name@then", dependsOn: when)
    tasks.test.dependsOn(then)

    featureClosure.delegate = [
        given: { Closure c -> given << c },
        then:  { Closure c -> then << c },
        when:  { Task t -> when.dependsOn(t.dependsOn(given)) }
    ] as FeatureDelegate
    featureClosure.resolveStrategy = Closure.DELEGATE_FIRST
    featureClosure.call()
}

task test {
}

apply from: 'acceptance-test.feature.gradle'
