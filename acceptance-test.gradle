description 'Automated acceptance test with SSH server'

buildscript {
    repositories {
        ivy { url = "$buildDir/ivy" }
        mavenCentral()
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:$version"
    }
    // avoid dependency resolution problem
    version = null
}

interface FeatureDelegate {
    void given(Closure givenClosure)
    void when(Map map, Closure whenClosure)
    void then(Closure thenClosure)
}

def feature(String name, Closure featureClosure) {
    Closure givenClosure = {}
    Closure thenClosure = {}
    featureClosure.delegate = [
        given: { Closure c -> givenClosure = c },
        then:  { Closure c -> thenClosure = c },
    ] as FeatureDelegate
    featureClosure.resolveStrategy = Closure.DELEGATE_FIRST

    final initialTasks = []
    initialTasks.addAll(tasks)

    featureClosure.call()

    final addedTasks = []
    addedTasks.addAll(tasks)
    addedTasks.removeAll(initialTasks)

    def given = task("$name@given") << givenClosure
    addedTasks.each { Task task -> task.dependsOn(given) }
    def when = task("$name@when", dependsOn: addedTasks)
    def then = task("$name@then", dependsOn: when) << thenClosure
    tasks.test.dependsOn(then)
}

task test {
}

apply from: 'acceptance-test.feature.gradle'
